PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <https://nickdrummond.github.io/star-wars-ontology/ontologies#>
SELECT DISTINCT ?person ?role ?association WHERE {
    {
    # Complex intersectionOf roles
    ?person rdf:type [
      rdf:type owl:Restriction ;
      owl:onProperty :hadRole ;
      owl:someValuesFrom ?roleClass
    ] .

    ?roleClass owl:intersectionOf ?list .
    ?list rdf:first ?role .
    ?list rdf:rest/rdf:first ?innerRestriction .

    ?innerRestriction owl:onProperty :inOrganisation ;
                      owl:hasValue ?association .
  }
  UNION
  {
    # Simple roles like :Jedi_Master
    ?person :memberOf ?association.
    ?person rdf:type [
      rdf:type owl:Restriction ;
      owl:onProperty :hadRole ;
      owl:someValuesFrom ?roleClass
    ] .
   ?roleClass a :Role.
  }
} ORDER BY ?person